5 REM BREAKIN
10 DN$="{home}{down*24}{right*39}"
20 SC=1024:NB=9:CB=54272:FORI=0TO24:POKECB+I,0:NEXT:GOTO2000
40 FORI=0TO24:POKECB+I,0:NEXT:GOTO100
50 REM SOUND ROUTINE
60 POKECB+24,15:POKECB+4,0:POKECB+6,222
70 POKECB+4,FF:POKECB+1,18:POKECB,9
80 FORT=1TO5:NEXT
90 POKECB,0:POKECB+1,0:RETURN
1000 REM MOVE BAT
1020 A=PEEK(197):IFA=64THEN1220
1050 PRINTLEFT$(DN$,BV)RIGHT$(DN$,BH);:FORI=1TOBS:PRINT" ";:NEXT
1060 BH=BH+((A=12)-(A=36))*2
1070 IFBH<8THENBH=8
1080 IFBH>38-BSTHENBH=38-BS
1090 PRINTLEFT$(DN$,BV)RIGHT$(DN$,BH);:FORI=1TOBS:PRINT"{black}{cm u}";:NEXT
1200 REM CHECK BOUNDARY HIT
1220 BX=BX+DX:BY=BY+DY:FF=21
1230 IFBX<9THENDX=-DX:GOSUB50:GOTO1520
1240 IFBX>36THENDX=-DX:GOSUB50:GOTO1520
1250 IFBY<1THENBY=1:DY=-DY:GOSUB50:GOTO1520
1260 IFBY>23THENBY=23:DY=-DY:GOSUB50
1500 REM CHECK BAT/BRCK HIT & PRINT BALL
1520 POKEBA,32:BA=SC+BY*40+BX
1530 IFPEEK(BA)=120THENBA=B1:DY=-DY:FF=133:GOSUB50:GOTO1000
1540 IFPEEK(BA)=207THENDY=-DY:FF=35:GOSUB50:GS=GS+1:PRINT"{home}{black}{down}"GS
1550 POKEBA,81:POKEBA+CB,0
1560 REM CHECK FOR MISSED BALL
1570 IFGM=1ANDBY>22ANDDY=-1THENNB=NB-1:PRINT"{home}{down*9}{black}"NB
1580 IFGM<>1ANDBY=12THENNB=NB-1:PRINT"{home}{down*9}{black}"NB
1590 IFNB<1THEN5000
1600 IFINT(GS/100)-(GS/100)<>0THEN1000
1610 ONGMGOSUB2650,2700,2800
2000 REM TITLE SCREEN
2020 POKE53281,3:POKE53280,3:PRINT"{clear}{down*4}{black}{space*17}Q"
2030 FORT=1TO100:NEXT
2040 PRINT"{clear}{down*5}{red}{space*17}Q{black}Q{purple}Q"
2050 FORT=1TO100:NEXT
2060 PRINT"{clear}{down*6}{red}{space*16}Q {black}Q {purple}Q"
2070 FORT=1TO100:NEXT
2080 PRINT"{clear}{down*7}{green}{space*15}Q{red}Q {black}Q {purple}Q{blue}Q"
2090 FORT=1TO100:NEXT
2100 PRINT"{clear}{down*8}{green}{space*14}Q {red}Q {black}Q {purple}Q {blue}Q"
2110 FORT=1TO100:NEXT
2120 PRINT"{clear}{down*9}{orange}{space*13}Q{green}Q {red}Q {black}Q {purple}Q {blue}Q{dark gray}Q"
2130 FORT=1TO100:NEXT
2140 PRINT"{clear}{down*10}{orange}{space*12}Q {green}Q {red}Q {black}Q {purple}Q {blue}Q {dark gray}Q"
2150 FORT=1TO200:NEXT:FF=35:GOSUB50
2160 PRINT"{clear}{down*10}{orange}{space*12}b {green}Q {red}Q {black}Q {purple}Q {blue}Q {dark gray}Q"
2170 FORT=1TO200:NEXT:FF=21:GOSUB50
2180 PRINT"{clear}{down*10}{orange}{space*12}b {green}r {red}Q {black}Q {purple}Q {blue}Q {dark gray}Q"
2190 FORT=1TO200:NEXT:FF=35:GOSUB50
2200 PRINT"{clear}{down*10}{orange}{space*12}b {green}r {red}e {black}Q {purple}Q {blue}Q {dark gray}Q"
2210 FORT=1TO200:NEXT:FF=21:GOSUB50
2220 PRINT"{clear}{down*10}{orange}{space*12}b {green}r {red}e {black}a {purple}Q {blue}Q {dark gray}Q"
2230 FORT=1TO200:NEXT:FF=35:GOSUB50
2240 PRINT"{clear}{down*10}{orange}{space*12}b {green}r {red}e {black}a {purple}k {blue}Q {dark gray}Q"
2250 FORT=1TO200:NEXT:FF=21:GOSUB50
2260 PRINT"{clear}{down*10}{orange}{space*12}b {green}r {red}e {black}a {purple}k {blue}i {dark gray}Q"
2270 FORT=1TO200:NEXT:FF=35:GOSUB50
2280 PRINT"{clear}{down*10}{orange}{space*12}b {green}r {red}e {black}a {purple}k {blue}i {dark gray}n"
2290 FORT=1TO200:NEXT:FF=21:PRINT"{down*2}"
2300 GOSUB50
2400 PRINT"{black}{space*8}{reverse on}select game number (1-3)":FORT=1TO90:NEXT
2410 PRINT"{up}{reverse off}{space*8}select game number (1-3)";:FORT=1TO90:NEXT
2420 GETGM$:IFGM$<"1"THENPRINT:PRINT"{up}";:GOTO2300
2430 IFGM$>"3"THENPRINT:PRINT"{up}";:GOTO2300
2440 GM=VAL(GM$):PRINT" "GM:PRINT:FF=35
2450 GOSUB50
2460 PRINT"{black}{space*9}{reverse on}select bat size (2-3)":FORT=1TO90:NEXT
2470 PRINT"{up}{reverse off}{space*9}select bat size (2-3)";:FORT=1TO90:NEXT
2480 GETBS$:IFBS$<"2"THENPRINT:PRINT"{up}";:GOTO2450
2490 IFBS$>"3"THENPRINT:PRINT"{up}";:GOTO2450
2500 BS=VAL(BS$):PRINT" "BS
2510 NB=9:GS=100*(4-BS)
2570 FORT=1TO500:NEXT
2580 PRINT"{clear}{black}{space*7}{reverse on}{space*12}breakin{space*13}"
2590 FORI=1TO23:PRINT"{space*7}{reverse on} {reverse off}{space*30}{reverse on} ":NEXT
2600 PRINT"{black}{space*7}{reverse on}{space*32}{home}"
2610 PRINT"{home}score":PRINTGS:PRINT"{down}high":PRINT"score":PRINTHS:PRINT"{down}balls"
2620 PRINT"left":PRINTNB:PRINT"{down}game":PRINT"number":PRINTGM
2630 PRINT"{down}bat":PRINT"size":PRINTBS
2640 ONGMGOTO2650,2700,2800
2650 REM   GAME 1
2655 PRINT"{home}{down*2}{green}"
2660 FORJ=1TO4:PRINT"{right*8}{reverse on}OOOOOOOOOOOOOOOOOOOOOOOOOOOOO":NEXT
2665 IFGS>0THENGS=GS+1
2666 IFSW>0THEN1000
2670 SW=1:BV=21:BH=17:BY=15:BX=18:BA=SC+BY*40+BX:DX=-1:DY=-1
2680 GOTO1050
2700 REM GAME 2
2710 PRINT"{home}{down*2}{red}"
2720 FORJ=1TO3:PRINT"{right*8}{reverse on}OOOOOOOOOOOOOOOOOOOOOOOOOOOOOO":NEXT
2730 PRINT"{down*12}"
2740 FORJ=1TO3:PRINT"{right*8}{reverse on}OOOOOOOOOOOOOOOOOOOOOOOOOOOOOO":NEXT
2745 IFGS>0THENGS=GS+1
2746 IFSW>0THEN1000
2750 SW=1:BV=13:BH=20:BY=10:BX=21:BA=SC+BY*40+BX:DX=1:DY=-1
2760 GOTO1050
2800 REM GAME3
2810 PRINT"{home}{down*2}"
2820 FORI=0TO8:PRINT"{right*10}";:FORJ=9-ITO1STEP-1:PRINT"{reverse on}{blue}O";:NEXT
2830 PRINTSPC(I*2+8);:FORJ=9-ITO1STEP-1:PRINT"O";:NEXT:PRINT:NEXT:PRINT
2850 FORI=0TO8:PRINT"{right*10}";:FORJ=1TOI+1:PRINT"{reverse on}{blue}O";:NEXT
2860 PRINTSPC(((8-I)*2)+8);:FORJ=1TOI+1:PRINT"O";:NEXT:PRINT:NEXT:PRINT"{home}"
2865 IFGS>0THENGS=GS+1
2866 IFSW>0THEN1000
2870 SW=1:BV=13:BH=16:BY=9:BX=24:BA=SC+BY*40+BX:DX=1:DY=-1
2880 GOTO1050
5000 REM END GAME
5020 PRINT"{home}{down*13}{right*10}{space*4}another game(y/n)"
5030 GETA$
5050 IFA$="n"THENPRINT"{clear}":END
5060 IFA$<>"y"THEN5030
5070 IFGS>HSTHENHS=GS
5080 SW=0:GOTO2280
