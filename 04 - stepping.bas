10 REM STEPPING STONES
20 MM$(0)="{reverse on}{blue}Q":MM$(1)="{reverse on}{blue} ":MM$(2)="{reverse on}{blue}Q":MM$(3)="{reverse on}{blue} ":MM$(4)="{reverse on}{blue}Q"
30 C=54272:POKE54296,15:POKE54276,0
40 BG$="{brown}{cm +*40}"
50 RR$="{reverse on}{white} {blue}{space*38}"
60 GOTO1000
100 PRINT"{home}{right*39}{down*3}";
101 FORI=1TO8:PRINT"{down}{left}";MM$(RND(.)*5);:NEXT
102 PRINT"{down}";
103 FORI=1TO8:PRINT"{down}{left}";MM$(RND(.)*5);:NEXT
104 RETURN
110 FORI=1TO8:PRINT"{right*2}"CHR$(20):NEXT:RETURN
130 GOSUB100
135 PRINT"{home}{down}{right*7}time="RIGHT$(TI$,4)"{right*5}score="SC
140 PRINT"{home}{down*4}";:GOSUB110:PRINT"{home}{down*13}";:GOSUB110
145 IFINT(ABS(R)/9)=0ANDR<>0THENP=P-1
150 K=PEEK(197)
160 IFK=64THENX=0:GOTO197
170 IFK=10THENX=-1:GOTO195
175 IFK=9THENX=-40:R=R-1:GOTO195
180 IFK=18THENX=1:GOTO195
185 IFK=12THENX=40:R=R+1
195 IFP+X>1903THENX=0
196 IFP+X<1144THENX=0
197 IFPEEK(P+X)=32ORPEEK(P+X)=160THEN600
200 IFP<1184THENPOKEP,102:GOTO205
201 IFP<1504THENPOKEP,209:GOTO205
202 IFP<1544THENPOKEP,102:GOTO205
203 IFP<1864THENPOKEP,209:GOTO205
204 IFP<1904THENPOKEP,102
205 IFX<>0THENPOKE54273,20:POKE54272,10:POKE54277,22:POKE54276,0:POKE54276,129
206 POKE54276,0:P=P+X:POKEP,170
207 IFP<1184ANDQ=0THENQ=1:SC=SC+INT(1000000/TI):TI$="000000":GOSUB300
208 IFP>1863ANDQ=1THENQ=0:SC=SC+INT(1000000/TI):TI$="000000":GOSUB300
210 GOTO130
300 POKE54276,0:POKE54278,240
310 POKE54276,33:POKE54273,6:POKE54272,206
320 FORT=1TO200:NEXT:POKE54273,0:POKE54272,0
330 POKE54278,0:RETURN
600 PRINT"{home}{down*33}{brown}"
610 PRINT"{space*2}drowned like a rat!":PRINT"{down*2}{space*10}score ="SC
620 PRINT"{down*2}{space*2}previous high ="PH
630 IFSC>PHTHENPH=SC
640 PRINT"{down*3}{space*2}another game (y/n){down*5}"
650 GETG$:IFG$="y"THEN1100
660 IFG$="n"THENPOKE54276,0:PRINT"{clear}":END
670 GOTO650
1000 POKE53280,1:POKE53281,1:PRINT"{clear}{brown}{down*3}{space*10}stepping stones"
1020 FORX=1TO1000:NEXT
1050 PRINT"{down*2}{right}your mission is to move back & forth "
1060 PRINT"{down} across the swollen raging river."
1070 PRINT"{down*2} tippy-toe on the magic stepping stones"
1075 PRINT"{down} using the keyboard characters:-"
1076 PRINT"{down*2}{space*16}w{down*2}{left*3}a Q d{down*2}{left*3}z"
1080 PRINT"{down*2}{space*9}hit key to begin."
1090 WAIT 198,1
1100 PRINT"{clear}"
1110 PRINTBG$BG$BG$;:FORI=1TO8:PRINTRR$:NEXT
1120 PRINTBG$;:FORI=1TO8:PRINTRR$:NEXT:PRINTBG$BG$BG$"{home}"
1130 P=1864+INT(RND(1)*35):POKEP,170:R=9:Q=0:SC=0:TI$="000000"
1140 GOTO130
